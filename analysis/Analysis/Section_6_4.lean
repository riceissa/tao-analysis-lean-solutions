import Mathlib.Tactic
import Analysis.Section_6_3

/-!
# Analysis I, Section 6.4

I have attempted to make the translation as faithful a paraphrasing as possible of the original
text. When there is a choice between a more idiomatic Lean solution and a more faithful
translation, I have generally chosen the latter. In particular, there will be places where the
Lean code could be "golfed" to be more elegant and idiomatic, but I have consciously avoided
doing so.

Main constructions and results of this section:

- Lim sup and lim inf of sequences
- Limit points of sequences
- Comparison and squeeze tests
- Completeness of the reals

-/

abbrev Real.adherent (ε:ℝ) (a:Chapter6.Sequence) (x:ℝ) := ∃ n ≥ a.m, ε.close (a n) x

abbrev Real.continually_adherent (ε:ℝ) (a:Chapter6.Sequence) (x:ℝ) :=
  ∀ N ≥ a.m, ε.adherent (a.from N) x

namespace Chapter6

abbrev Sequence.limit_point (a:Sequence) (x:ℝ) : Prop :=
  ∀ ε > (0:ℝ), ε.continually_adherent a x

theorem Sequence.limit_point_def (a:Sequence) (x:ℝ) :
  a.limit_point x ↔ ∀ ε > 0, ∀ N ≥ a.m, ∃ n ≥ N, |a n - x| ≤ ε := by
    unfold limit_point Real.continually_adherent Real.adherent
    sorry

noncomputable abbrev Example_6_4_3 : Sequence := (fun (n:ℕ) ↦ 1 - (10:ℝ)^(-(n:ℤ)-1))

/-- Example 6.4.3 -/
example : (0.1:ℝ).adherent Example_6_4_3 0.8 := by sorry

/-- Example 6.4.3 -/
example : ¬ (0.1:ℝ).continually_adherent Example_6_4_3 0.8 := by sorry

/-- Example 6.4.3 -/
example : (0.1:ℝ).continually_adherent Example_6_4_3 1 := by sorry

/-- Example 6.4.3 -/
example : Example_6_4_3.limit_point 1 := by sorry

noncomputable abbrev Example_6_4_4 : Sequence :=
  (fun (n:ℕ) ↦ (-1:ℝ)^n * (1 + (10:ℝ)^(-(n:ℤ)-1)))

/-- Example 6.4.4 -/
example : (0.1:ℝ).adherent Example_6_4_4 1 := by sorry

/-- Example 6.4.4 -/
example : (0.1:ℝ).continually_adherent Example_6_4_4 1 := by sorry

/-- Example 6.4.4 -/
example : Example_6_4_4.limit_point 1 := by sorry

/-- Example 6.4.4 -/
example : Example_6_4_4.limit_point (-1) := by sorry

/-- Example 6.4.4 -/
example : ¬ Example_6_4_4.limit_point 0 := by sorry

/-- Proposition 6.4.5 / Exercise 6.4.1 -/
theorem Sequence.limit_point_of_limit {a:Sequence} {x:ℝ} (h: a.tendsTo x) : a.limit_point x := by
  sorry

/--
  A technical issue uncovered by the formalization: the upper and lower sequences of a real
  sequence take values in the extended reals rather than the reals, so the definitions need to be
  adjusted accordingly.
-/
noncomputable abbrev Sequence.upperseq (a:Sequence) : ℤ → EReal := fun N ↦ (a.from N).sup

noncomputable abbrev Sequence.limsup (a:Sequence) : EReal :=
  sInf { x | ∃ N ≥ a.m, x = a.upperseq N }

noncomputable abbrev Sequence.lowerseq (a:Sequence) : ℤ → EReal := fun N ↦ (a.from N).inf

noncomputable abbrev Sequence.liminf (a:Sequence) : EReal :=
  sSup { x | ∃ N ≥ a.m, x = a.lowerseq N }

noncomputable abbrev Example_6_4_7 : Sequence := (fun (n:ℕ) ↦ (-1:ℝ)^n * (1 + (10:ℝ)^(-(n:ℤ)-1)))

example (n:ℕ) :
    Example_6_4_7.upperseq n = if Even n then 1 + (10:ℝ)^(-(n:ℤ)-1) else 1 + (10:ℝ)^(-(n:ℤ)-2) := by
  sorry

example : Example_6_4_7.limsup = 1 := by sorry

example (n:ℕ) :
    Example_6_4_7.lowerseq n
    = if Even n then -(1 + (10:ℝ)^(-(n:ℤ)-2)) else -(1 + (10:ℝ)^(-(n:ℤ)-1)) := by
  sorry

example : Example_6_4_7.liminf = -1 := by sorry

example : Example_6_4_7.sup = (1.1:ℝ) := by sorry

example : Example_6_4_7.inf = (-1.01:ℝ) := by sorry

noncomputable abbrev Example_6_4_8 : Sequence := (fun (n:ℕ) ↦ if Even n then (n+1:ℝ) else -(n:ℝ)-1)

example (n:ℕ) : Example_6_4_8.upperseq n = ⊤ := by sorry

example : Example_6_4_8.limsup = ⊤ := by sorry

example (n:ℕ) : Example_6_4_8.lowerseq n = ⊥ := by sorry

example : Example_6_4_8.liminf = ⊥ := by sorry

noncomputable abbrev Example_6_4_9 : Sequence :=
  (fun (n:ℕ) ↦ if Even n then (n+1:ℝ)⁻¹ else -(n+1:ℝ)⁻¹)

example (n:ℕ) : Example_6_4_9.upperseq n = if Even n then (n+1:ℝ)⁻¹ else -(n+2:ℝ)⁻¹ := by sorry

example : Example_6_4_9.limsup = 0 := by sorry

example (n:ℕ) : Example_6_4_9.lowerseq n = if Even n then -(n+2:ℝ)⁻¹ else -(n+1:ℝ)⁻¹ := by sorry

example : Example_6_4_9.liminf = 0 := by sorry

noncomputable abbrev Example_6_4_10 : Sequence := (fun (n:ℕ) ↦ (n+1:ℝ))

example (n:ℕ) : Example_6_4_10.upperseq n = ⊤ := by sorry

example : Example_6_4_9.limsup = ⊤ := by sorry

example (n:ℕ) : Example_6_4_9.lowerseq n = n+1 := by sorry

example : Example_6_4_9.liminf = ⊤ := by sorry

/-- Proposition 6.4.12(a) -/
theorem Sequence.gt_limsup_bounds {a:Sequence} {x:EReal} (h: x > a.limsup) :
    ∃ N ≥ a.m, ∀ n ≥ N, a n < x := by
  -- This proof is written to follow the structure of the original text.
  unfold Sequence.limsup at h
  simp [sInf_lt_iff] at h
  obtain ⟨aN, ⟨ N, ⟨ hN, haN ⟩ ⟩, ha ⟩ := h
  use N
  simp [hN]
  simp [haN, Sequence.upperseq] at ha
  intro n hn
  have hn' : n ≥ (a.from N).m := by simp [hN, hn]
  convert lt_of_le_of_lt ((a.from N).le_sup hn') ha using 1
  simp [hn, hN.trans hn]

/-- Proposition 6.4.12(a) -/
theorem Sequence.lt_liminf_bounds {a:Sequence} {y:EReal} (h: y < a.liminf) :
    ∃ N ≥ a.m, ∀ n ≥ N, a n > y := by
  sorry

/-- Proposition 6.4.12(b) -/
theorem Sequence.lt_limsup_bounds {a:Sequence} {x:EReal} (h: x < a.limsup) {N:ℤ} (hN: N ≥ a.m) :
    ∃ n ≥ N, a n > x := by
  -- This proof is written to follow the structure of the original text.
  unfold Sequence.limsup at h
  have hx : x < a.upperseq N := by
    apply lt_of_lt_of_le h (sInf_le _)
    simp; use N
  unfold Sequence.upperseq at hx
  replace hx := Sequence.exists_between_lt_sup hx
  obtain ⟨ n, hn, hxn, _ ⟩ := hx
  simp [Sequence.from, hN] at hn
  use n, hn
  convert gt_iff_lt.mpr hxn using 1
  simp [hn, hN.trans hn]

/-- Proposition 6.4.12(b) -/
theorem Sequence.gt_liminf_bounds {a:Sequence} {x:EReal} (h: x > a.liminf) {N:ℤ} (hN: N ≥ a.m) :
    ∃ n ≥ N, a n < x := by
  sorry

/-- Proposition 6.4.12(c) / Exercise 6.4.3 -/
theorem Sequence.inf_le_liminf (a:Sequence) : a.inf ≤ a.liminf := by sorry

/-- Proposition 6.4.12(c) / Exercise 6.4.3 -/
theorem Sequence.liminf_le_limsup (a:Sequence) : a.liminf ≤ a.limsup := by sorry

/-- Proposition 6.4.12(c) / Exercise 6.4.3 -/
theorem Sequence.limsup_le_sup (a:Sequence) : a.limsup ≤ a.sup := by sorry

/-- Proposition 6.4.12(d) / Exercise 6.4.3 -/
theorem Sequence.limit_point_between_liminf_limsup {a:Sequence} {c:ℝ} (h: a.limit_point c) :
  a.liminf ≤ c ∧ c ≤ a.limsup := by
  sorry

/-- Proposition 6.4.12(e) / Exercise 6.4.3 -/
theorem Sequence.limit_point_of_limsup {a:Sequence} {L_plus:ℝ} (h: a.limsup = L_plus) :
    a.limit_point L_plus := by
  sorry

/-- Proposition 6.4.12(e) / Exercise 6.4.3 -/
theorem Sequence.limit_point_of_liminf {a:Sequence} {L_minus:ℝ} (h: a.liminf = L_minus) :
    a.limit_point L_minus := by
  sorry

/-- Proposition 6.4.12(f) / Exercise 6.4.3 -/
theorem Sequence.tendsTo_iff_eq_limsup_liminf {a:Sequence} (c:ℝ) :
  a.tendsTo c ↔ a.liminf = c ∧ a.limsup = c := by
  sorry

/-- Lemma 6.4.13 (Comparison principle) / Exercise 6.4.4 -/
theorem Sequence.sup_mono {a b:Sequence} (hm: a.m = b.m) (hab: ∀ n ≥ a.m, a n ≤ b n) :
    a.sup ≤ b.sup := by sorry

/-- Lemma 6.4.13 (Comparison principle) / Exercise 6.4.4 -/
theorem Sequence.inf_mono {a b:Sequence} (hm: a.m = b.m) (hab: ∀ n ≥ a.m, a n ≤ b n) :
    a.inf ≤ b.inf := by sorry

/-- Lemma 6.4.13 (Comparison principle) / Exercise 6.4.4 -/
theorem Sequence.limsup_mono {a b:Sequence} (hm: a.m = b.m) (hab: ∀ n ≥ a.m, a n ≤ b n) :
    a.limsup ≤ b.limsup := by sorry

/-- Lemma 6.4.13 (Comparison principle) / Exercise 6.4.4 -/
theorem Sequence.liminf_mono {a b:Sequence} (hm: a.m = b.m) (hab: ∀ n ≥ a.m, a n ≤ b n) :
    a.liminf ≤ b.liminf := by sorry

/-- Corollary 6.4.14 (Squeeze test) / Exercise 6.4.5 -/
theorem Sequence.lim_of_between {a b c:Sequence} {L:ℝ} (hm: b.m = a.m ∧ c.m = a.m)
  (hab: ∀ n ≥ a.m, a n ≤ b n ∧ b n ≤ c n) (ha: a.tendsTo L) (hb: b.tendsTo L) :
    c.tendsTo L := by sorry

/-- Example 6.4.15 -/
example : ((fun (n:ℕ) ↦ 2/(n+1:ℝ)):Sequence).tendsTo 0 := by
  sorry

/-- Example 6.4.15 -/
example : ((fun (n:ℕ) ↦ -2/(n+1:ℝ)):Sequence).tendsTo 0 := by
  sorry

/-- Example 6.4.15 -/
example : ((fun (n:ℕ) ↦ (-1)^n/(n+1:ℝ) + 1 / (n+1)^2):Sequence).tendsTo 0 := by
  sorry

/-- Example 6.4.15 -/
example : ((fun (n:ℕ) ↦ (2:ℝ)^(-(n:ℤ))):Sequence).tendsTo 0 := by
  sorry

abbrev Sequence.abs (a:Sequence) : Sequence where
  m := a.m
  seq := fun n ↦ |a n|
  vanish := by
    intro n hn
    simp [a.vanish n hn]


/-- Corollary 6.4.17 (Zero test for sequences) / Exercise 6.4.7 -/
theorem Sequence.tendsTo_zero_iff (a:Sequence) :
  a.tendsTo (0:ℝ) ↔ a.abs.tendsTo (0:ℝ) := by
  sorry

/--
  This helper lemma, implicit in the textbook proofs of Theorem 6.4.18 and Theorem 6.6.8, is made
  explicit here.
-/
theorem Sequence.finite_limsup_liminf_of_bounded {a:Sequence} (hbound: a.isBounded) :
    (∃ L_plus:ℝ, a.limsup = L_plus) ∧ (∃ L_minus:ℝ, a.liminf = L_minus) := by
  obtain ⟨ M, hMpos, hbound ⟩ := hbound
  unfold Sequence.BoundedBy at hbound
  have hlimsup_bound : a.limsup ≤ M := by
    apply a.limsup_le_sup.trans
    apply sup_le_upper
    intro n hN
    simp
    exact (le_abs_self _).trans (hbound n)
  have hliminf_bound : -M ≤ a.liminf := by
    apply LE.le.trans _ a.inf_le_liminf
    apply inf_ge_lower
    intro n hN
    simp [←EReal.coe_neg]
    rw [neg_le]
    exact (neg_le_abs _).trans (hbound n)
  constructor
  . use a.limsup.toReal
    apply (EReal.coe_toReal _ _).symm
    . contrapose! hlimsup_bound
      simp [hlimsup_bound]
    replace hliminf_bound := hliminf_bound.trans a.liminf_le_limsup
    contrapose! hliminf_bound
    simp [hliminf_bound, ←EReal.coe_neg]
  use a.liminf.toReal
  apply (EReal.coe_toReal _ _).symm
  . replace hlimsup_bound := a.liminf_le_limsup.trans hlimsup_bound
    contrapose! hlimsup_bound
    simp [hlimsup_bound]
  contrapose! hliminf_bound
  simp [hliminf_bound, ←EReal.coe_neg]

/-- Theorem 6.4.18 (Completeness of the reals) -/
theorem Sequence.Cauchy_iff_convergent (a:Sequence) :
  a.isCauchy ↔ a.convergent := by
  -- This proof is written to follow the structure of the original text.
  refine ⟨ ?_, Cauchy_of_convergent ⟩
  intro h
  obtain ⟨ ⟨ L_plus, hL_plus ⟩, ⟨ L_minus, hL_minus ⟩ ⟩ :=
    finite_limsup_liminf_of_bounded (bounded_of_cauchy h)
  use L_minus
  rw [tendsTo_iff_eq_limsup_liminf]
  simp [hL_minus, hL_plus]
  have hlow : 0 ≤ L_plus - L_minus := by
    have := a.liminf_le_limsup
    simp [hL_minus, hL_plus] at this
    linarith
  have hup (ε:ℝ) (hε: ε>0) : L_plus - L_minus ≤ 2*ε := by
    specialize h ε hε
    obtain ⟨ N, hN, hsteady ⟩ := h
    unfold Real.steady Real.close at hsteady
    have hN0 : N ≥ (a.from N).m := by
      simp [Sequence.from, hN]
    have hN1 : (a.from N).seq N = a.seq N := by
      simp [Sequence.from, hN]
    have h1 : (a N - ε:ℝ) ≤ (a.from N).inf := by
      apply inf_ge_lower
      intro n hn
      specialize hsteady n hn N hN0
      rw [ge_iff_le, EReal.coe_le_coe_iff]
      rw [Real.dist_eq, hN1, abs_le'] at hsteady
      linarith
    have h2 : (a.from N).inf ≤ L_minus := by
      simp_rw [←hL_minus, Sequence.liminf, Sequence.lowerseq]
      apply le_sSup
      simp; use N
    have h3 : (a.from N).sup ≤ (a N + ε:ℝ) := by
      apply sup_le_upper
      intro n hn
      specialize hsteady n hn N hN0
      rw [EReal.coe_le_coe_iff]
      rw [Real.dist_eq, hN1, abs_le'] at hsteady
      linarith
    have h4 : L_plus ≤ (a.from N).sup := by
      simp_rw [←hL_plus, Sequence.limsup, Sequence.upperseq]
      apply sInf_le
      simp; use N
    replace h1 := h1.trans h2
    replace h4 := h4.trans h3
    rw [EReal.coe_le_coe_iff] at h1 h4
    linarith
  rcases le_iff_lt_or_eq.mp hlow with hlow | hlow
  . specialize hup ((L_plus - L_minus)/3) (by positivity)
    linarith
  linarith






/-- Exercise 6.4.6 -/
theorem Sequence.sup_not_strict_mono : ∃ (a b:ℕ → ℝ), (∀ n, a n < b n) ∧ (a:Sequence).sup ≠ (b:Sequence).sup := by
  sorry

/- Exercise 6.4.7 -/

def Sequence.tendsTo_real_iff :
  Decidable (∀ (a:Sequence) (x:ℝ), a.tendsTo x ↔ a.abs.tendsTo x) := by
  -- The first line of this construction should be `apply isTrue` or `apply isFalse`.
  sorry

/-- This definition is needed for Exercises 6.4.8 and 6.4.9. -/
abbrev Sequence.extended_limit_point (a:Sequence) (x:EReal) : Prop := if x = ⊤ then ¬ a.bddAbove else if x = ⊥ then ¬ a.bddBelow else a.limit_point x.toReal


/-- Exercise 6.4.8 -/
theorem Sequence.extended_limit_point_of_limsup (a:Sequence) : a.extended_limit_point a.limsup := by sorry

/-- Exercise 6.4.8 -/
theorem Sequence.extended_limit_point_of_liminf (a:Sequence) : a.extended_limit_point a.liminf := by sorry

theorem Sequence.extended_limit_point_le_limsup {a:Sequence} {L:EReal} (h:a.extended_limit_point L): L ≤ a.limsup := by sorry

theorem Sequence.extended_limit_point_ge_liminf {a:Sequence} {L:EReal} (h:a.extended_limit_point L): L ≥ a.liminf := by sorry

/-- Exercise 6.4.9 -/
theorem Sequence.exists_three_limit_points : ∃ a:Sequence, ∀ L:EReal, a.extended_limit_point L ↔ L = ⊥ ∨ L = 0 ∨ L = ⊤ := by sorry

/-- Exercise 6.4.10 -/
theorem Sequence.limit_points_of_limit_points {a b:Sequence} {c:ℝ} (hab: ∀ n ≥ b.m, a.limit_point (b n)) (hbc: b.limit_point c) : a.limit_point c := by sorry


end Chapter6
